/******************************************************************************
 * Copyright (c) 2011 Reinaldo Junior.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *    Reinaldo Junior (Google Summer of Code) - initial API and implementation
 ******************************************************************************/

grammar org.eclipse.amalgam.tutorials.xtext.Droid with org.eclipse.xtext.common.Terminals
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate droid "http://www.eclipse.org/amalgam/tutorials/xtext/Droid"


Application:
	'application' name=STRING packageName=QualifiedName
	'{'
		(manifest=ManifestFile)?
		(resources+=Resource | layouts+=Layout | activities+=Activity)+
	'}'
;


/*

manifest {
	userPermission {
		com.android.CONTACT_LIST,
		com.android.GPS
	}
	
	sdk {
		from 1 to 5
		target 5
	}
	
	requires {
		fiveWayNav
		hardKeyboard
		keyboardType
		navigation
		touchScreen
	}
	
	intents {
	}
	
	libraries {
	}

}

*/

//PENDING
ManifestFile:
	{ManifestFile}
	'manifest' '{'
	'}'
;

Layout:
	'layout' {Layout} name=ID '{'
		widgets+=Widget*
	'}'
;


Widget:
	(GenericWidget | Button | ImageButton | Link | Spinner | EditText | TextView) //';'
;

GenericWidget:
	'widget' {GenericWidget} ('<'id=ID'>')?
	name=STRING
	(
	 ('top:' top=DimensionPropertyValue )? &
	 ('left:' left=DimensionPropertyValue )? &
	 ('width:' width=DimensionPropertyValue )? &
	 ('height:' height=DimensionPropertyValue )? &
	 ('background:' background=DrawableResourcePropertyValue )? &
	 ('clickable?' clickable=BooleanPropertyValue )? &
	 ('fadeScrollBars?' fadeScrollBars=BooleanPropertyValue )? &
	 ('isScrollContainer?' isScrollContainer=BooleanPropertyValue )?
	 // Specific properties
	)
	('->' actions+=Action )*
;

TextView:
	'textView' {TextView} ('<'id=ID'>')?
	text=StringPropertyValue
	(
	 ('top:' top=DimensionPropertyValue )? &
	 ('left:' left=DimensionPropertyValue )? &
	 ('width:' width=DimensionPropertyValue )? &
	 ('height:' height=DimensionPropertyValue )? &
	 ('background:' background=DrawableResourcePropertyValue )? &
	 ('clickable?' clickable=BooleanPropertyValue )? &
	 ('fadeScrollBars?' fadeScrollBars=BooleanPropertyValue )? &
	 ('isScrollContainer?' isScrollContainer=BooleanPropertyValue )? &
	 // Specific properties
	 ('hint:' hint=StringPropertyValue)? &
	 ('textColor:' textColor=ColorPropertyValue)?
	)
	('->' actions+=Action )*
;

Button:
	'button' {Button} ('<'id=ID'>')?
	text=StringPropertyValue
	(
	 ('top:' top=DimensionPropertyValue )? &
	 ('left:' left=DimensionPropertyValue )? &
	 ('width:' width=DimensionPropertyValue )? &
	 ('height:' height=DimensionPropertyValue )? &
	 ('background:' background=DrawableResourcePropertyValue )? &
	 ('clickable?' clickable=BooleanPropertyValue )? &
	 ('fadeScrollBars?' fadeScrollBars=BooleanPropertyValue )? &
	 ('isScrollContainer?' isScrollContainer=BooleanPropertyValue )? &
	 // Specific properties
	 ('hint:' hint=StringPropertyValue)? &
	 ('textColor:' textColor=ColorPropertyValue)?
	)
	('->' actions+=Action )*
;

ImageButton:
	'imageButton' {ImageButton} ('<'id=ID'>')?
	src=DrawableImageResourcePropertyValue
	(
	 ('top:' top=DimensionPropertyValue )? &
	 ('left:' left=DimensionPropertyValue )? &
	 ('width:' width=DimensionPropertyValue )? &
	 ('height:' height=DimensionPropertyValue )? &
	 ('background:' background=DrawableResourcePropertyValue )? &
	 ('clickable?' clickable=BooleanPropertyValue )? &
	 ('fadeScrollBars?' fadeScrollBars=BooleanPropertyValue )? &
	 ('isScrollContainer?' isScrollContainer=BooleanPropertyValue )? &
	 // Specific properties
	 ('hint:' hint=StringPropertyValue)? &
	 ('textColor:' textColor=ColorPropertyValue)?
	)
	('->' actions+=Action )*
;

//Button to an Activity
Link:
	'link' {Link} ('<'id=ID'>')?
	text=StringPropertyValue
	(
	 ('top:' top=DimensionPropertyValue )? &
	 ('left:' left=DimensionPropertyValue )? &
	 ('width:' width=DimensionPropertyValue )? &
	 ('height:' height=DimensionPropertyValue )? &
	 ('background:' background=DrawableResourcePropertyValue )? &
	 ('clickable?' clickable=BooleanPropertyValue )? &
	 ('fadeScrollBars?' fadeScrollBars=BooleanPropertyValue )? &
	 ('isScrollContainer?' isScrollContainer=BooleanPropertyValue )? &
	 // Specific properties
	 ('hint:' hint=StringPropertyValue)? &
	 ('textColor:' textColor=ColorPropertyValue)?
	)
	'to' target=[Activity]
	('->' actions+=Action )*
;

//--Parei aqui


Spinner:
	'spinner' {Spinner} ('<'id=ID'>')?
	(
	 ('top:' top=DimensionPropertyValue )? &
	 ('left:' left=DimensionPropertyValue )? &
	 ('width:' width=DimensionPropertyValue )? &
	 ('height:' height=DimensionPropertyValue )? &
	 ('background:' background=DrawableResourcePropertyValue )? &
	 ('clickable?' clickable=BooleanPropertyValue )? &
	 ('fadeScrollBars?' fadeScrollBars=BooleanPropertyValue )? &
	 ('isScrollContainer?' isScrollContainer=BooleanPropertyValue )? &
	 //Spinner properties
	 ('prompt:' prompt=StringPropertyValue)?
	)
;

EditText:
	'editText' {EditText} ('<'id=ID'>')?
	text=StringPropertyValue

	(
	 ('top:' top=DimensionPropertyValue )? &
	 ('left:' left=DimensionPropertyValue )? &
	 ('width:' width=DimensionPropertyValue )? &
	 ('height:' height=DimensionPropertyValue )? &
	 ('background:' background=DrawableResourcePropertyValue )? &
	 ('clickable?' clickable=BooleanPropertyValue )? &
	 ('fadeScrollBars?' fadeScrollBars=BooleanPropertyValue )? &
	 ('isScrollContainer?' isScrollContainer=BooleanPropertyValue )? &
	 //EditText properties
	 ('hint:' hint=StringPropertyValue)? &
	 ('textColor:' textColor=ColorPropertyValue)? &
	 ('editable?' editable=BooleanPropertyValue)? &
	 ('numeric?' numeric=BooleanPropertyValue)? &
	 ('password?' password=BooleanPropertyValue)? &
	 ('phoneNumber?' phoneNumber=BooleanPropertyValue)?
	)
;



Activity:
	(GenericActivity | ListActivity | TabActivity)
;

GenericActivity:
	'activity' name=QualifiedName '{'
		(actions+=Action)*
	'}'
;

ListActivity:
	'listActivity' name=QualifiedName '{'
		'data' dataSource=ID
		('layout' layout=[Layout])?
		'item' itemLayout=[Layout]
		(actions+=Action)*
	'}'

;

TabActivity:
	'tabActivity' name=QualifiedName '{'
		(tabs+=Tab)+
	'}'
;

Tab:
	'tab' activity=[Activity]
;


Action:
	(GenericAction | GoToURLAction /* | LoadURLAction */ | ShowLayoutAction | InvokeActivityAction | LoadResourceAction)
;

/*
Action

ActionCondicional
 -> Action(semCondicao) + condicao
	('if' condition+=Action )?
*/


/*
//ExtraComplexity
ConditionalAction
contition=Conditional

Conditional
firstCondition+=Conditional
(operador=Operator
secondCondition+=Conditional)?
*/

GenericAction:
	'action'
	name=QualifiedName
;

GoToURLAction:
	'goTo'
	url=URL
;

ShowLayoutAction:
	'show'
	layout=[Layout]
;

InvokeActivityAction:
	'invoke'
	activity=[Activity|QualifiedName]
;



//Loads a fixed resource
//TODO: Load a dinamic resource from a URL, loadJson for example
//MAYBE...
LoadResourceAction:
	'load' resource=ID
	('->' toVar=ID)?
	('=>' afterLoad=Action)?
;


/*
 * RESOURCES
 */


Resource:
	(MenuResource | ValueResource | DrawableResource /*| AnimationResource */ )
;

//PENDING
//Implement attributes: http://developer.android.com/guide/topics/resources/menu-resource.html
MenuResource:
	'menu' {MenuResource} name=ID '{'
		(menuItems+=MenuItem | subMenus+=SubMenu | groups+=MenuItemGroup)*
	'}'
;

MenuItem:
	'item' {MenuItem} '{'
		subMenus+=SubMenu*
	'}'
;

MenuItemGroup:
	'group' {MenuGroup} '{'
		items+=MenuItem+
	'}'
;

SubMenu:
	'submenu' {SubMenu} '{'
		(menuItems+=MenuItem | groups+=MenuItemGroup)*
	'}'
;

ValueResource:
  (
	StringValueResource | IntegerValueResource |
	BooleanValueResource | ColorValueResource |
	DimensionValueResource  | ArrayValueResource
  )
;

StringValueResource:
	'string' name=ID '=' value=STRING
;

IntegerValueResource:
	'integer' name=ID '=' value=INT
;

BooleanValueResource:
	'bool' name=ID '=' value=BOOL
;

ColorValueResource:
	'color' name=ID '=' value=HEX_COLOR
;

DimensionValueResource:
	'dimension' name=ID '=' value=FLOAT unit=DimensionKind
;

ArrayValueResource:
	'array'
	(IntegerArrayValueResource | StringArrayValueResource | TypedArrayValueResource)
;

IntegerArrayValueResource:
	'(integer)' {IntegerArrayValueResource}
	name=ID '=' '['
		(items+=INT (',' items+=INT)* )?
	']'
;

StringArrayValueResource:
	'(string)' {StringArrayValueResource}
	name=ID '=' '['
		(items+=STRING (',' items+=STRING)* )?
	']'
;

//I should create something like ResourceRefecence to represent this concept.
//ResourceRefecenre:
//internalResource=[ValueResource] | '(' externalResource=ID ')' ;
TypedArrayValueResource:
	{TypedArrayValueResource}
	name=ID '=' '['
//		(items+=PropertyValue (',' items+=PropertyValue)* )?
	']'
;

//http://developer.android.com/guide/topics/resources/drawable-resource.html
DrawableResource:
	(BitmapDrawableResource | TransitionDrawableResource)
;

//http://developer.android.com/guide/topics/resources/drawable-resource.html#Bitmap
BitmapDrawableResource:
	'bitmap' {BitmapDrawableResource} name=ID '=' filename=ID
;

//http://developer.android.com/guide/topics/resources/drawable-resource.html#Transition
TransitionDrawableResource:
	'transition' {TransitionDrawableResource} from=[BitmapDrawableResource] '<->' to=[BitmapDrawableResource]
;


/*
 * Value of Properties which can be set using resources
 */

PropertyValue:
	(
	  StringPropertyValue | IntegerPropertyValue |
	  BooleanPropertyValue | ColorPropertyValue |
	  DimensionPropertyValue | /*
	  IntegerArrayPropertyValue | StringArrayPropertyValue |
	  Typed ArrayPropertyValue */
	  DrawableResourcePropertyValue
	)
;

StringPropertyValue:
	(value=STRING | resource=[StringValueResource] | externalResource=ExternalResourceReference )
;

IntegerPropertyValue:
	(value=INT | resource=[IntegerValueResource] | externalResource=ExternalResourceReference)
;

BooleanPropertyValue:
	(value=BOOL | resource=[BooleanValueResource] | externalResource=ExternalResourceReference)
;

ColorPropertyValue:
	(value=HEX_COLOR | resource=[ColorValueResource] | externalResource=ExternalResourceReference)
;

DimensionPropertyValue:
	(value=FLOAT | resource=[DimensionValueResource] | externalResource=ExternalResourceReference)
;

DrawableResourcePropertyValue:
	(value=HEX_COLOR | resource=[DrawableResource] | externalResource=ExternalResourceReference)
;

DrawableImageResourcePropertyValue:
	(resource=[DrawableResource] | externalResource=ExternalResourceReference)
;

//'(' customInterpolator=ID ')' t‡ dando muita left-recursion (acho que d‡ ambiguidade nessa droga)
InterpolatorPropertyValue:
	 shareInterpolator?='shared'
	 //Se der problema, tira o externalResource=ExternalResourceReference
	 'interpolator' (externalResource=ExternalResourceReference | name=InterpolatorsKind )
	//(predefinedInterpolator=InterpolatorsKind | '(' customInterpolator=ID ')')
;

/*
 * I think I should oversimplify the animation IF i'm going to include it. Reason: A LOT OF Left-recursion problems
 */
/*

AnimationResource:
	( TweenAnimationResource | FrameAnimationResource)
;


AnimationTerminalElements:
	(AlphaAnimation | ScaleAnimation | TranslateAnimation | RotateAnimation)
;


//AnimationElement:
//	(AnimationSet | AnimationTerminalElements)
//;


//Nao tire senao d‡ merda
AbstractAnimationSet:
	TerminalAnimationSet | AnimationSet
;

TerminalAnimationSet:
	'set' '{' {TerminalAnimationSet}
		//Isso chegou a dar problema, agora n‹o mais
		interpolator=InterpolatorPropertyValue
		(animationElements+=AnimationTerminalElements)+
	'}'
;

AnimationSet:
	'set' '{' {AnimationSet}
	 	interpolator=InterpolatorPropertyValue
		//(animationElements+=AnimationTerminalElements)+
		(animationElements+=AnimationTerminalElements | animationElements+=TerminalAnimationSet )+
	'}'
;

AlphaAnimation:
	'fade:'
	(
	 ('from' fromAlpha=FLOAT) &
	 ('to' toAlpha=FLOAT)
	)
;

ScaleAnimation:
	'scale:'
	(
	 ('from' fromAlpha=FLOAT) &
	 ('to' toAlpha=FLOAT)
	)
;

TranslateAnimation:
	'translate:'
	(
	 ('from' fromAlpha=FLOAT) &
	 ('to' toAlpha=FLOAT)
	)
;

RotateAnimation:
	'rotate:'
	(
	 ('from' fromAlpha=FLOAT) &
	 ('to' toAlpha=FLOAT)
	)
;


TweenAnimationResource:
	'tweenAnimation' name=ID '{'
		(animationElements+=AnimationTerminalElements | animationElements+=TerminalAnimationSet)+
	'}'
;

FrameAnimationResource:
	oneShot?='oneShot' 'frameAnimation' name=ID '{'
		frames+=AnimationFrame+
	'}'
;

AnimationFrame:
	'frame:' drawable=DrawableResourcePropertyValue ('->' duration=INT)?
;
*/












enum DimensionKind :
  dp | sp | pt | px | mm | in
;

enum InterpolatorsKind :
  accelerate_decelerate |
  accelerate |
  anticipate |
  anticipate_overshoot |
  bounce |
  cycle |
  decelerate |
  linear |
  overshoot
;

//data type rule (returns EDataType instead EClass)
//this one dont need a value converter because it handles string
QualifiedName:
	ID ('.' ID)*;

ExternalResourceReference:
	'[' id=ID ']'
;

//Value Converter
BOOL returns ecore::EBooleanObject:
//	'YES' | 'NO'
	'true' | 'false'
;

/*
RelativePercentual:
	INT ('%'|'%p')
;
*/

FLOAT returns ecore::EFloatObject:
	INT ('.' INT)?
;

terminal HEX_COLOR:
	'#' 
	    ('0'..'9'|'A'..'F'|'a'..'f') ('0'..'9'|'A'..'F'|'a'..'f')
	    ('0'..'9'|'A'..'F'|'a'..'f') ('0'..'9'|'A'..'F'|'a'..'f')
	    ('0'..'9'|'A'..'F'|'a'..'f') ('0'..'9'|'A'..'F'|'a'..'f')
	    (('0'..'9'|'A'..'F'|'a'..'f') ('0'..'9'|'A'..'F'|'a'..'f'))? 
;

URL:
	ID '://' QualifiedName ('/'  (QualifiedName '/')* )?
;